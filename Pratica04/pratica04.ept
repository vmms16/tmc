node rad(up1, down1, up2, down2, c1, c2 : bool) returns (power: int)
let
	automaton
		state OFF do
			power = 0;
		unless up1 or not c1 then FROST
		state FROST do
			power = 300;
		unless up1 & c1 then ECO
			| down1 or not c1 then OFF
			| up2 & c2 then HIGHT
		state ECO do 
			power = 1500;
		unless down1 or not c1 then FROST
			| up2 & c2 then HIGHT
		state HIGHT do
			power = 2000;
		unless down1 & not c1 then ECO
			| down2 & not c2 then FROST
	end
tel

type spl = S1|S2|S3

node police(eco, confort: bool) returns (pl: int; s: spl)
let
	automaton
		state MS do
			pl = 3000;
			s = S1;
		unless eco then ECO
			|confort then CONFORT
		state ECO do
			pl = 5000;
			s = S2;
		unless not eco then MS
			| confort then CONFORT
		state CONFORT do
			pl= 7000;
			s = S3;
		unless not confort then MS
			| eco then ECO
	end
tel

node ws( s, e ,c:bool) returns (power: int)
let
	automaton
		state OFF do
			power = 0;
		unless s then WF
		state WF do
			power = 15;
		unless e & c then WASHING
			| e & not c then STANDBY
		state WASHING do
			power = 250;
		unless e then RINSE
		state RINSE do
			power = 200;
		unless e then SPIN
		state SPIN do
			power = 800;
		unless e then OFF
		state STANDBY do
			power = 0;
		unless c then WASHING
	end
tel

node oven(finish, start, cold, temp_ok, c: bool) returns(power: int)
let
	automaton
		state OFF do
			power=0;
		unless start & not c then STANDBY
			| start & c then HEAT
		state HEAT do
			power=1500;
		unless finish then OFF
			| temp_ok then MAINTAIN
		state STANDBY do
			power=0;
		unless c then HEAT
		state MAINTAIN do
			power=300;
		unless cold & c then REHEAT
			| finish then OFF
		state REHEAT do
			power=1000;
		unless temp_ok then MAINTAIN
			| finish then OFF
	end
tel



node task(up1, down1, up2, down2, s, e, finish, start, cold, temp_ok, eco, confort: bool) returns(pl,p1, p2, p3: int; sp: spl)
	contract
		var rule5: bool;
		let
			switch sp
				| S1 do rule5 = p1+p2+p3 <= 3000
				| S2 do rule5 = p1+p2+p3 <= 5000
				| S3 do rule5 = p1+p2+p3 <= 7000
			end 
		tel 
	 enforce (rule5)
	with(c1,c2,c3,c4: bool)
let
	(pl, sp)= inlined police(eco, confort);
	p1 = inlined rad(up1, down1, up2, down2, c1, c2);
	p2 = inlined ws(s, e, c3);
	p3 = inlined oven(finish, start, cold, temp_ok, c4)
tel